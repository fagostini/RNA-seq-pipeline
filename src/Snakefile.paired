# Import config file & parameters
configfile: 'config.yaml'

rule all:
	input:
		expand("fastqc/{sample}_{ext}_fastqc.html", sample=config["samples"], ext=["F", "R"]),
		expand("picarded/{sample}.Aligned.sortedByCoord.picard.bam.bai", sample=config["samples"]),
		expand("qorts/{sample}.firstStrand.counts", sample=config["samples"]),
		expand("qorts/{sample}.secondStrand.counts", sample=config["samples"]),
		expand("coverage/{sample}.{strand}.bedGraph", sample=config["samples"], strand=["pos", "neg"])

rule fastqc:
	input:
		fastq=lambda wildcards: config["samples"][wildcards.sample]		
	output:
		html=expand("fastqc/{{sample}}_{ext}_fastqc.html", ext=["F", "R"]),
		gzip=temp(expand("fastqc/{{sample}}_{ext}_fastqc.zip", ext=["F", "R"]))
	params:
		fc="--outdir fastqc",
		cluster='-N 1 -c 4 --mem=8G -t 160:00:00'
	threads:
		4
	run:
		shell("fastqc -t {threads} {params.fc} {input.fastq}")
		fastqc_out = "/".join(("fastqc", os.path.basename(input["fastq"][0])))
		fastqc_out = "_".join((fastqc_out.partition(".")[0], "fastqc.html"))
		fastqc_html = output["html"][0]
		shell("mv {fastqc_out} {fastqc_html}")
		fastqc_out = "/".join(("fastqc", os.path.basename(input["fastq"][1])))
		fastqc_out = "_".join((fastqc_out.partition(".")[0], "fastqc.html"))
		fastqc_html = output["html"][1]
		shell("mv {fastqc_out} {fastqc_html}")
		zip_out = "/".join(("fastqc", os.path.basename(input["fastq"][0])))
		zip_out = "_".join((zip_out.partition(".")[0], "fastqc.zip"))
		fastqc_gzip = output["gzip"][0]
		shell("mv {zip_out} {fastqc_gzip}")
		zip_out = "/".join(("fastqc", os.path.basename(input["fastq"][1])))
		zip_out = "_".join((zip_out.partition(".")[0], "fastqc.zip"))
		fastqc_gzip = output["gzip"][1]
		shell("mv {zip_out} {fastqc_gzip}")

rule trimgalore:
	input:
		fastq=lambda wildcards: config["samples"][wildcards.sample]
	output:
		fastq=expand("trim_galore/{{sample}}_{ext}_trimmed.fq.gz", ext=["F", "R"])
	params:
		basic="-q 20 --gzip --length 16 --no_report_file",
		paired="--paired --trim1",
		outputfolder="-o trim_galore",
		cluster='-N 1 -c 1 --mem=16G -t 160:00:00'
	log:
		log="logs/trim_galore/{sample}.fq.gz_trimming_report.txt",
	run:
		shell("trim_galore {params.basic} {params.paired} {params.outputfolder} {input} &> {log}")
		trimmed_out = "/".join(("trim_galore", os.path.basename(input["fastq"][0])))
		trimmed_out = "_".join((trimmed_out.partition(".")[0], "val_1.fq.gz"))
		final_out = output["fastq"][0]
		shell("mv {trimmed_out} {final_out}")
		trimmed_out = "/".join(("trim_galore", os.path.basename(input["fastq"][1])))
		trimmed_out = "_".join((trimmed_out.partition(".")[0], "val_2.fq.gz"))
		final_out = output["fastq"][1]
		shell("mv {trimmed_out} {final_out}")

rule index_rRNA_tRNA:
	input:
		"genome/human_rRNA_tRNA/rRNA_tRNA.fa.gz"
	output:
		expand("genome/human_rRNA_tRNA/rRNA_tRNA.{count}.bt2", count=['1', '2', '3', '4']),
		expand("genome/human_rRNA_tRNA/rRNA_tRNA.rev.{count}.bt2", count=['1', '2'])
	params:
		basename="genome/human_rRNA_tRNA/rRNA_tRNA",
		cluster='-N 1 -c 8 --mem=30G -t 160:00:00'
	log:
		"logs/index_rRNA_tRNA/index_rRNA_tRNA.log"
	threads:
		8
	shell:
		"bowtie2-build --threads {threads} {input} {params.basename} &> {log}"

rule remove_rRNA_tRNA:
	input:
		fastq=expand("trim_galore/{{sample}}_{ext}_trimmed.fq.gz", ext=["F", "R"]),
		index1=expand("genome/human_rRNA_tRNA/rRNA_tRNA.{count}.bt2", count=['1', '2', '3', '4']),
		index2=expand("genome/human_rRNA_tRNA/rRNA_tRNA.rev.{count}.bt2", count=['1', '2'])
	output:
		pairs=expand("rRNA_tRNA_removed/{{sample}}.{ext}.rRNA_tRNA_removed.fq.gz", ext=["F", "R"]),
		single=temp("rRNA_tRNA_removed/{sample}.rRNA_tRNA_removed.fq.gz"),
		alignedS=temp("rRNA_tRNA_removed/{sample}.rRNA_tRNA_aligned.fq.gz"),
		alignedP=temp(expand("rRNA_tRNA_removed/{{sample}}.rRNA_tRNA_aligned.fq.{ext}.gz", ext=["1", "2"])),
		tmp=temp("rRNA_tRNA_removed/{sample}.tmp")
	params:
		cluster='-N 1 -c 8 --mem=16G -t 160:00:00'
	log:
		"logs/rRNA_tRNA_removed/{sample}.rRNA_tRNA_removed.log"
	threads:
		8
	run:
		fq1 = input["fastq"][0] 
		fq2 = input["fastq"][1]
		shell("bowtie2 -p {threads} --un-conc-gz {output.single} --un-gz {output.single} --al-gz {output.alignedS} --al-conc-gz {output.alignedS} -x genome/human_rRNA_tRNA/rRNA_tRNA -1 {fq1} -2 {fq2} 1> {output.tmp} 2> {log}")
		fq_out = ".".join((output["single"].partition(".gz")[0], "1.gz"))
		final_out = output["pairs"][0]
		shell("mv {fq_out} {final_out}")
		fq_out = ".".join((output["single"].partition(".gz")[0], "2.gz"))
		final_out = output["pairs"][1]
		shell("mv {fq_out} {final_out}")

rule gunzip_annotation:
	input:
		"annotation/gencode.v27.annotation.gtf.gz"
	output:
		"annotation/gencode.v27.annotation.gtf"
	params:
		cluster='-N 1 -c 1 --mem=8G -t 160:00:00'
	shell:
		"gunzip {input}"

rule gunzip_genome:
	input:
		"genome/GRCh38.primary_assembly.genome.fa.gz"
	output:
		"genome/GRCh38.primary_assembly.genome.fa"
	params:
		cluster='-N 1 -c 1 --mem=8G -t 160:00:00'
	shell:
		"gunzip {input}"

rule index_star:
	input:
		genome="genome/GRCh38.primary_assembly.genome.fa",
		annotation="annotation/gencode.v27.annotation.gtf"
	output:
		"genome/STAR_GRCh38_v27",
		"genome/STAR_GRCh38_v27/SA",
		"genome/STAR_GRCh38_v27/SAindex",
		"genome/STAR_GRCh38_v27/chrNameLength.txt"
	params:
		prefix="genome/STAR_GRCh38_v27",
		overhang="--sjdbOverhang 99",
		cluster='-N 1 -c 8 --mem=64G -t 160:00:00'
	threads:
		8
	shell:
		"""
		STAR --runMode genomeGenerate --runThreadN {threads} --genomeDir {params.prefix} --genomeFastaFiles {input.genome} --sjdbGTFfile {input.annotation} {params.overhang}
		gzip genome/GRCh38.primary_assembly.genome.fa
		gzip annotation/gencode.v27.annotation.gtf
		"""

rule map_star:
	input:
		fastq=expand("rRNA_tRNA_removed/{{sample}}.{ext}.rRNA_tRNA_removed.fq.gz", ext=["F", "R"]),
		index="genome/STAR_GRCh38_v27/SAindex",
		genome="genome/STAR_GRCh38_v27"
	output:
		bam="mapped/{sample}.Aligned.sortedByCoord.out.bam",
		counts="mapped/{sample}.ReadsPerGene.out.tab",
		junctions="mapped/{sample}.SJ.out.tab",
		log1=temp("mapped/{sample}.Log.out"),
		log2=temp("mapped/{sample}.Log.progress.out"),
		genomefolder=temp("mapped/{sample}._STARgenome"),
		passfolder=temp("mapped/{sample}._STARpass1")
	params:
		outprefix="mapped/{sample}.",
		alignemnt="--readFilesCommand zcat --genomeLoad NoSharedMemory --twopassMode Basic --alignSJoverhangMin 8 --alignSJDBoverhangMin 1 --alignIntronMin 20 --alignIntronMax 1000000 --alignMatesGapMax 1000000 --quantMode GeneCounts --sjdbScore 1 --limitBAMsortRAM 60000000000",
		output="--outFilterMultimapNmax 1 --outFilterMismatchNmax 999 --outFilterMismatchNoverReadLmax 0.04 --outFilterType BySJout  --outSAMattributes All --outSAMstrandField intronMotif --outSAMtype BAM SortedByCoordinate",
		log="mapped/{sample}.Log.final.out",
		cluster='-N 1 -c 8 --mem=64G -t 160:00:00'
	log:
		"logs/map_star/{sample}.Log.final.out"
	threads:
		8
	shell:
		"""
		STAR --runMode alignReads --runThreadN {threads} --genomeDir {input.genome} --readFilesIn {input.fastq} --outFileNamePrefix {params.outprefix} {params.alignemnt} {params.output} &> {log}
		mv {params.log} {log}
		"""

#rule get_picard:
#	input:
#		HTTP.remote("https://github.com/broadinstitute/picard/releases/download/2.17.0/picard.jar", keep_local=True)
#	output:
#		"picard.jar"
#	run:
#		outputName = os.path.basename(input[0])
#		shell("mv {input} {outputName}")
#		shell("chmod a+x {outputName}")

rule remove_duplicate:
	input:
#		picard="picard.jar",
		bamfile="mapped/{sample}.Aligned.sortedByCoord.out.bam"
	output:
		"picarded/{sample}.Aligned.sortedByCoord.picard.bam",
	log:
		"logs/remove_duplicate/{sample}.Aligned.picard.txt"
	params:
		pd="REMOVE_DUPLICATES=true ASSUME_SORT_ORDER=coordinate",
		ss="SORT_ORDER=coordinate",
		cluster='-N 1 -c 1 --mem=30G -t 160:00:00'
	shell:
		"java -Xmx16G -jar picard.jar MarkDuplicates I={input.bamfile} O=/dev/stdout M={log} {params.pd} | java -Xmx16G -jar picard.jar SortSam I=/dev/stdin O={output} {params.ss}"

rule picard_index:
	input:
		"picarded/{sample}.Aligned.sortedByCoord.picard.bam"
	output:
		"picarded/{sample}.Aligned.sortedByCoord.picard.bam.bai"
	params:
		cluster='-N 1 -c 1 --mem=16G -t 160:00:00'
	shell:
		"java -Xmx16G -jar picard.jar BuildBamIndex I={input} O={output}"

rule create_flatGFF:
	input:
		"annotation/gencode.v27.annotation.gtf.gz"
	output:
		"annotation/gencode.v27.annotation.flat.gtf.gz"
	params:
		cluster='-N 1 -c 1 --mem=16G -t 160:00:00'
	shell:
		"java -Xmx16G -jar QoRTs.jar makeFlatGff --stranded {input} {output}"

#rule get_qorts:
#	input:
#		HTTP.remote("https://github.com/hartleys/QoRTs/releases/download/1.3.0/QoRTs.jar", keep_local=True, allow_redirects=True)
#	output:
#		"QoRTs.jar"
#	run:
#		outputName = os.path.basename(input[0])
#		shell("mv {input} {outputName}")
#		shell("chmod a+x {outputName}")

rule quantification:
	input:
#		executable="QoRTs.jar",
		gff="annotation/gencode.v27.annotation.gtf.gz",
		flatfile="annotation/gencode.v27.annotation.flat.gtf.gz",
		bamfile="picarded/{sample}.Aligned.sortedByCoord.picard.bam",
		index="picarded/{sample}.Aligned.sortedByCoord.picard.bam.bai",
		chrmlen="genome/STAR_GRCh38_v27/chrNameLength.txt"
	output:
		firstStrand="qorts/{sample}.firstStrand.counts",
		secondStrand="qorts/{sample}.secondStrand.counts"
	params:
		library="--stranded --minMAPQ 255",
		functions="StrandCheck,GeneCalcs,writeKnownSplices,writeNovelSplices,chromCounts,writeGenewiseGeneBody,writeBiotypeCounts,calcDetailedGeneCounts,makeWiggles,writeDESeq,writeDEXSeq,writeJunctionSeqCounts",
		cluster='-N 1 -c 1 --mem=16G -t 160:00:00'
	shell:
		"""
		java -Xmx16G -jar QoRTs.jar QC {params.library} --runFunctions {params.functions} --chromSizes {input.chrmlen} {input.bamfile} {input.gff} {output.firstStrand}
		java -Xmx16G -jar QoRTs.jar QC {params.library} --stranded_fr_secondstrand --runFunctions {params.functions} --chromSizes {input.chrmlen} {input.bamfile} {input.gff} {output.secondStrand}
		"""

rule genome_coverage:
	input:
		bamfile="picarded/{sample}.Aligned.sortedByCoord.picard.bam"
	output:
		positive="coverage/{sample}.pos.bedGraph",
		negative="coverage/{sample}.neg.bedGraph"
	params:
		bt="-bg -split",
		paired="-pc -du",
		cluster='-N 1 -c 1 --mem=48G -t 160:00:00'
	shell:
		"""
		genomeCoverageBed {params.bt} {params.paired} -strand '+' -ibam {input.bamfile} > {output.positive}
		genomeCoverageBed {params.bt} {params.paired} -strand '-' -ibam {input.bamfile} > {output.negative}
		"""
